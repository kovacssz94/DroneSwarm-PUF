(* Channels *)
free c: channel. 

(* Define types for IDs, nonces, messages, and MACs etc. *)
type nonce.
type ID.
type message.
type mac_val.  
type puf_response.
type init_points.

(* Type conversions *)
fun IDToMsg(ID): message.
fun PufToMsg(puf_response): message.

(*Initial events*)
event Drone_Start(ID, nonce, nonce).
event Leader_Forward(ID, nonce, nonce).
event GS_Auth_Di_DL(ID, ID, message, ID, nonce, nonce).
(* Events for authentication *) 
event Leader_Receives_ACK(ID, ID, message, nonce, nonce).
event Drone_Auth_Complete(ID, ID, message, nonce, nonce).

(* Define cryptographic functions and primitives *)
fun H(puf_response, nonce): ID. (* generating AID *)
fun compute_mac(ID, puf_response, nonce, nonce): mac_val.(* MAC_i and MAC_L*)
fun compute_mac2(ID, puf_response, nonce, nonce, mac_val): mac_val.(* MAC_i and MAC_L*)
fun compute_mac_GS(message, ID, puf_response, nonce, message, nonce): mac_val. (* MAC_GS with more values *)
fun compute_mac_L(ID, message, message, message, nonce, nonce): mac_val. (* MAC_L2  *)
fun PUF_response(nonce): puf_response.
fun hash(message, message): message.
fun hash2(init_points, message): message.
fun F(puf_response): message.

(* DEBUG 
free test:bitstring [private].
query attacker(test).*)


query AID_i: ID, AID_L: ID, ACK_i: message, ACK_L: ID, ts3: nonce, ts1: nonce, session: nonce;
  inj-event(GS_Auth_Di_DL(AID_i, AID_L, ACK_i, ACK_L, ts3, session)) ==>
    (inj-event(Leader_Forward(AID_i, ts1, session)) ==>
     inj-event(Drone_Start(AID_i, ts1, session))).

 
query AID_i: ID, AID_L: ID, ACK_i: message, ACK_L: ID, Auth_L: message, ts4: nonce, ts3: nonce, session: nonce;
  inj-event(Drone_Auth_Complete(AID_i, AID_L, ACK_i, ts4, session)) ==>
    (inj-event(Leader_Receives_ACK(AID_i, ACK_L, Auth_L, ts4, session)) ==>
     inj-event(GS_Auth_Di_DL(AID_i, AID_L, ACK_i, ACK_L, ts3, session))).

table replay_table(nonce, nonce).
table ts1_table(nonce, nonce).
table session_table(nonce, nonce).
table old_session_table(nonce). 

(* Drone *)
let drone(Di_Challenge: nonce) =
  let R_i = PUF_response(Di_Challenge) in
  (* Generate session key *)
  new session: nonce;
  new session_key: nonce;
  insert session_table(session_key, session);
  (* SEND message 1 *)
  new ts1: nonce;
  let AID_i = H(R_i, ts1) in
  let MAC_i = compute_mac(AID_i, R_i, ts1, session) in
  let MAC_full = compute_mac2(AID_i, R_i, ts1, session, MAC_i) in
  event Drone_Start(AID_i, ts1, session);
  out(c, (AID_i, MAC_i, ts1, session_key, MAC_full));

  (* Drone finish *)
  (* GET message 4 *)
  in(c, (AID_L: ID, Auth_L: message, ACK_i: message, Msg_GS: message, MAC_L2: mac_val, ts4: nonce, MAC: mac_val));
  (* Timestamp validation *)
  get replay_table(tsX, session_keyX) suchthat (tsX = ts4 && session_keyX = session_key) in
    0 (* Replay detected — already used *)
  else
    insert replay_table(ts4, session_key);
  get old_session_table(sessionX) suchthat (sessionX = session) in
    0 (* Replay detected — already used *)
  else
    insert old_session_table(session);
    (* MAC validation *)
    if MAC = compute_mac2(AID_i, R_i, ts1, session, MAC_i) then
      if compute_mac_L(AID_L, Auth_L, ACK_i, Msg_GS, ts4, session) = MAC_L2 then 
        (* Authenticate DL and GS *)
        if ACK_i = hash(PufToMsg(R_i), Auth_L) then 
          event Drone_Auth_Complete(AID_i, AID_L, ACK_i, ts4, session).

(* Leader *)
let leader(DL_Challenge: nonce) =
  let R_L = PUF_response(DL_Challenge) in
  (* GET message 1 *)
  in(c, (AID_i: ID, MAC_i: mac_val, ts1: nonce, session_key: nonce));
  get session_table(=session_key, session) in
  (* Timestamp validation of message 1 *)
  get replay_table(tsX, session_keyX) suchthat (tsX = ts1 && session_keyX = session_key) in
    0 (* Replay detected — already used *)
  else
    insert replay_table(ts1, session_key); 
  get old_session_table(sessionX) suchthat (sessionX = session) in
    0 (* Replay detected — already used *)
  else
  (* SEND message 2 *)
  new ts2: nonce;
  let AID_L = H(R_L, ts2) in
  let MAC_L = compute_mac(AID_L, R_L, ts2, session) in
  event Leader_Forward(AID_i, ts1, session);
  out(c, (AID_i, MAC_i, AID_L, MAC_L, ts1, ts2, session_key));
  
  (* Leader auth *)
  (* GET message 3 *)
  in(c, (ACK_i: message, ACK_L: ID, Msg_GS: message, MAC_GS: mac_val, ts3: nonce));
  (* Timestamp validation of message 1 *)
  get replay_table(tsX, session_keyX) suchthat (tsX = ts3 && session_keyX = session_key) in
    0 (* Replay detected — already used *)
  else
    insert replay_table(ts3, session_key); 
  (* MAC verification *)
  if compute_mac_GS(ACK_i, ACK_L, R_L, session, Msg_GS, ts3) = MAC_GS then 
    (* Authenticate Di and GS *)
    if ACK_L = H(R_L, ts3) then 
      (* SEND message 4 *)
      new ts4: nonce;
      let Auth_L = hash(PufToMsg(R_L), IDToMsg(ACK_L)) in
      let MAC_L2 = compute_mac_L(AID_L, Auth_L, ACK_i, Msg_GS, ts4, session) in
      event Leader_Receives_ACK(AID_i, ACK_L, Auth_L, ts4, session);
      out(c, (AID_L, Auth_L, ACK_i, Msg_GS, MAC_L2, ts4)).

(* Ground Station *)
let ground_station(Di_Challenge: nonce, DL_Challenge: nonce, Points: init_points) =
  let R_i = PUF_response(Di_Challenge) in
  let R_L = PUF_response(DL_Challenge) in
  (* GET message 2 *)
  in(c, (AID_i: ID, MAC_i: mac_val, AID_L: ID, MAC_L: mac_val, ts1: nonce, ts2: nonce, session_key: nonce));
  get session_table(=session_key, session) in
  (* Timestamp validation *)
  get ts1_table(tsX, session_keyX) suchthat (tsX = ts1 && session_keyX = session_key) in
    0 (* Replay detected — already used *)
  else
    insert ts1_table(ts1, session_key);
  get replay_table(tsY, session_keyY) suchthat (tsY = ts2 && session_keyY = session_key) in
    0 (* Replay detected — already used *)
  else
    insert replay_table(ts2, session_key);
  get old_session_table(sessionX) suchthat (sessionX = session) in
    0 (* Replay detected — already used *)
  else
  (* Di and DL authentication *)
  if compute_mac(AID_i, R_i, ts1, session) = MAC_i && compute_mac(AID_L, R_L, ts2, session) = MAC_L then 
    if AID_i = H(R_i, ts1) && AID_L = H(R_L, ts2) then 
      (* SEND message 3 *)
      new ts3: nonce;
      let ACK_L = H(R_L, ts3) in
      let ACK_i = hash(PufToMsg(R_i), hash(PufToMsg(R_L), IDToMsg(ACK_L))) in
      let Y_i = F(R_i) in
      let Msg_GS = hash2(Points, Y_i) in
      let MAC_GS = compute_mac_GS(ACK_i, ACK_L, R_L, session, Msg_GS, ts3) in
      event GS_Auth_Di_DL(AID_i, AID_L, ACK_i, ACK_L, ts3, session);
      out(c, (ACK_i, ACK_L, Msg_GS, MAC_GS, ts3)).

process
! new Di_Challenge: nonce;
! new DL_Challenge: nonce;
! new Points: init_points;
  (* Launch an unbounded number of sessions of the other 3 participants *)
  !drone(Di_Challenge) |
  !leader(DL_Challenge) |
  !ground_station(Di_Challenge, DL_Challenge, Points)  

	