(* Public channel used for communication *)
free c_pub: channel.
free c_sec: channel [private].

(* Define types for IDs, nonces, messages, and MACs etc. *)
type nonce.
type ID.
type key.
type message.
type mac_val.  
type timestamp.
type puf_response.
type init_points.

(* Define cryptographic functions and primitives *)
fun H(puf_response, nonce): ID. (* generating AID *)
fun compute_mac(ID, puf_response, nonce): mac_val.(* MAC_i and MAC_L*)
fun compute_mac_GS(message, ID, puf_response, nonce, message): mac_val. (* MAC_GS with more values *)
fun compute_mac_L(message, message, nonce, nonce, message): mac_val. (* MAC_L2  *)
fun PUF_response(nonce): puf_response.
fun hash(message, message): message.
fun hash2(init_points, message): message.
fun hTime(nonce): message.
fun fst(message): message.
fun snd(message): message.
fun f(puf_response): message.
fun actualTime(): nonce.

(* Type conversions *)
fun IDToMsg(ID): message.
fun MsgToID(message): ID.
fun MsgToMac_val(message): mac_val.
fun MsgToTime(message): timestamp.
fun PufToMsg(puf_response): message.
fun tsToMessage(timestamp): message.
fun PointsToMsg(init_points): message.

(*Initial events*)
event Drone_Start().
event Leader_Forward().
(* Events for authentication *) 
event Di_Auth_DL_GS(puf_response, message, message).
event DL_Auth_Di_GS(puf_response, nonce, ID).
event GS_Auth_Di_DL(ID, ID).

(* DEBUG 
free test:bitstring [private].
query attacker(test).*)

(* Queries 
  Drone_Start() <== Leader_Forward() <== GS_Auth_Di_DL()
  GS_Auth_Di_DL() <== DL_Auth_Di_GS() <== Di_Auth_DL_GS() 
*)

query AID_i: ID, AID_L: ID; 
  inj-event(GS_Auth_Di_DL(AID_i, AID_L)) ==> 
    (inj-event(Leader_Forward()) ==>
      inj-event(Drone_Start())).

query R_L: puf_response, T_GS: nonce, ACK_L: ID, R_i: puf_response, ACK_i: message, Auth_L: message, AID_i: ID, AID_L: ID; 
  inj-event(Di_Auth_DL_GS(R_i, ACK_i, Auth_L)) ==>
    (inj-event(DL_Auth_Di_GS(R_L, T_GS, ACK_L)) ==>
      inj-event(GS_Auth_Di_DL(AID_i, AID_L))).

(*
query R_L: puf_response, T_GS: nonce, ACK_L: ID, R_i: puf_response, ACK_i: message, Auth_L: message, AID_i: ID, AID_L: ID; 
  inj-event(Di_Auth_DL_GS(R_i, ACK_i, Auth_L)) ==>
    (inj-event(DL_Auth_Di_GS(R_L, T_GS, ACK_L)) ==>
      (inj-event(GS_Auth_Di_DL(AID_i, AID_L)) ==>
        (inj-event(Leader_Forward()) ==>
          inj-event(Drone_Start())))).
*)

(* Drone Process - start  *)
let drone_start(Di_Challenge: nonce) =
  let R_i = PUF_response(Di_Challenge) in
  new T_i: nonce;
  let hT_i = hTime(T_i) in
  let AID_i = H(R_i, T_i) in
  let MAC_i = compute_mac(AID_i, R_i, T_i) in
  out(c_sec, T_i);
  out(c_pub, (AID_i, hT_i, MAC_i));
  event Drone_Start().

(* Leader Drone Process - Forward *)
let leader_first(DL_Challenge: nonce) =
  let R_L = PUF_response(DL_Challenge) in 
  new T_L: nonce;
  let hT_L = hTime(T_L) in
  let AID_L = H(R_L, T_L) in
  in(c_pub, (AID_i: ID, hT_i: message, MAC_i: mac_val));
  in(c_sec, T_i: nonce);
  (* Timestamp validation *)
  if hT_i = hTime(T_i) then  
    let MAC_L = compute_mac(AID_L, R_L, T_L) in
    out(c_sec, (T_i, T_L));
    out(c_pub, (AID_i, hT_i, MAC_i, AID_L, hT_L, MAC_L));
    event Leader_Forward().

(* Ground Station Process - Authentication *)
let ground_station(Di_Challenge: nonce, DL_Challenge: nonce, Points: init_points) =
  let R_i = PUF_response(Di_Challenge) in
  let R_L = PUF_response(DL_Challenge) in
  new T_GS: nonce;
  let hT_GS = hTime(T_GS) in
  in(c_sec, (T_i: nonce, T_L: nonce));
  in(c_pub, (AID_i: ID, hT_i: message, MAC_i: mac_val, AID_L: ID, hT_L: message, MAC_L:mac_val));
  (* Timestamp validation *)
  if hT_i = hTime(T_i) && hT_L = hTime(T_L) then
    (* MAC verification Di and DL *)
    if compute_mac(AID_i, R_i, T_i) = MAC_i && compute_mac(AID_L, R_L, T_L) = MAC_L then 
      (* Di and DL authentication *)
      if AID_i = H(R_i, T_i) && AID_L = H(R_L, T_L) then 
        let ACK_L = H(R_L, T_GS) in
        let ACK_i = hash(PufToMsg(R_i), hash(PufToMsg(R_L), IDToMsg(ACK_L))) in
        let Y_i = f(R_i) in
        let Msg_GS = hash2(Points, Y_i) in
        let MAC_GS = compute_mac_GS(ACK_i, ACK_L, R_L, T_GS, Msg_GS) in
        out(c_sec, T_GS);
        out(c_pub, (ACK_i, ACK_L, hT_GS, Msg_GS, MAC_GS));
        event GS_Auth_Di_DL(AID_i, AID_L).

(* Leader Drone Process - Authentication *)
let leader_auth(DL_Challenge: nonce) =
  let R_L = PUF_response(DL_Challenge) in
  new T_L2: nonce;
  let hT_L2 = hTime(T_L2) in
  in(c_sec, T_GS: nonce);
  in(c_pub, (ACK_i: message, ACK_L: ID, hT_GS: message, Msg_GS: message, MAC_GS: mac_val));
  if hT_GS = hTime(T_GS) then 
    (* MAC verification *)
    if compute_mac_GS(ACK_i, ACK_L, R_L, T_GS, Msg_GS) = MAC_GS then (*CANNOT BE PROVED*)
      (* Authenticate Di and GS *)
      if ACK_L = H(R_L, T_GS) then 
        let Auth_L = hash(PufToMsg(R_L), IDToMsg(ACK_L)) in
        let MAC_L2 = compute_mac_L(Auth_L, ACK_i, T_GS, T_L2, Msg_GS) in
        out(c_sec, (T_GS, T_L2));
        out(c_pub, (Auth_L, ACK_i, hT_GS, hT_L2, Msg_GS, MAC_L2));
        event DL_Auth_Di_GS(R_L, T_GS, ACK_L).

(* Drone Process - Authentication *)
let drone_auth(Di_Challenge: nonce) =
  let R_i = PUF_response(Di_Challenge) in 
  in(c_sec, (T_GS: nonce, T_L2: nonce));
  in(c_pub, (Auth_L: message, ACK_i: message, hT_GS: message, hT_L2: message, Msg_GS: message, MAC_L2: mac_val));
  (* Timestamp validation *)
  if hT_GS = hTime(T_GS) && hT_L2 = hTime(T_L2) then 
    (* MAC verification *)
    if compute_mac_L(Auth_L, ACK_i, T_GS, T_L2, Msg_GS) = MAC_L2 then 
      (* Authenticate DL and GS *)
      if ACK_i = hash(PufToMsg(R_i), Auth_L) then 
        event Di_Auth_DL_GS(R_i, ACK_i, Auth_L).

(* Swarm Initialization Process *)
process
  new Di_Challenge: nonce;
  new DL_Challenge: nonce;
  new Points: init_points;
  (
    !drone_start(Di_Challenge) |
    !leader_first(DL_Challenge) |
    !ground_station(Di_Challenge, DL_Challenge, Points) |
    !leader_auth(DL_Challenge) |
    !drone_auth(Di_Challenge) (**)
  )
